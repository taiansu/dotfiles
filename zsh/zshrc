### profile config
# zmodload zsh/zprof

# ghostty
# Ghostty shell integration for Bash. This should be at the top of your bashrc!
if [[ "$TERM_PROGRAM" == "ghostty" && -n "${GHOSTTY_RESOURCES_DIR}" ]]; then
    builtin source "${GHOSTTY_RESOURCES_DIR}/shell-integration/zsh/ghostty-integration"
fi

declare -A homebrew_prefix
homebrew_prefix[x86_64]=/usr/local
homebrew_prefix[arm64]=/opt/homebrew

#####

# Load completion functions
autoload -Uz compinit bashcompinit

# Define helper function for compilation
function zcompile-many() {
  local f
  for f; do zcompile -R -- "$f".zwc "$f"; done
}

# Determine dump file location
zcompdump="${ZDOTDIR:-$HOME}/.zcompdump"

# Initialize based on dump file age
if [[ -n "$zcompdump"(#qN.mh+24) ]]; then
  # Dump file is older than 24 hours, recreate it
  compinit
  bashcompinit
else
  # Dump file is recent, skip security checks for speed
  compinit -C
  bashcompinit
fi

# Compile dump file if needed
[[ "$zcompdump.zwc" -nt "$zcompdump" ]] || zcompile-many "$zcompdump"

# Clean up
unfunction zcompile-many
unset zcompdump

#####

autoload -Uz promptinit colors
colors
promptinit

autoload -Uz url-quote-magic
zle -N self-insert url-quote-magic
autoload -Uz bracketed-paste-url-magic
zle -N bracketed-paste bracketed-paste-url-magic
autoload backward-delete-word
zle -N backward-delete-word

# zsh-autosuggestions
[[ -s $homebrew_prefix[$CPUTYPE]/share/zsh-autosuggestions/zsh-autosuggestions.zsh ]] && source $homebrew_prefix[$CPUTYPE]/share/zsh-autosuggestions/zsh-autosuggestions.zsh

# zsh-history-substring-search
[[ -s $homebrew_prefix[$CPUTYPE]/share/zsh-history-substring-search/zsh-history-substring-search.zsh ]] && source $homebrew_prefix[$CPUTYPE]/share/zsh-history-substring-search/zsh-history-substring-search.zsh

if [[ test_declare(history-substring-search-up) ]]; then
  bindkey "$terminfo[kcuu1]" history-substring-search-up
  bindkey "$terminfo[kcud1]" history-substring-search-down
fi

# zsh-f-sy-h
[[ -s $homebrew_prefix[$CPUTYPE]/share/zsh-f-sy-h/F-Sy-H.plugin.zsh ]] && source $homebrew_prefix[$CPUTYPE]/share/zsh-f-sy-h/F-Sy-H.plugin.zsh

ZSH_AUTOSUGGEST_MANUAL_REBIND=1

# Default colors:
# White for users, red for root, yellow for system users
local _time="%F{246}[%*]"
# PROMPT_COMMAND='PS1X=$(p="${PWD#${HOME}}"; [ "${PWD}" != "${p}" ] && printf "~";IFS=/; for q in ${p:1}; do printf /${q:0:1}; done; printf "${q:1}")'
local _path="%F{blue}%(4~|%-1~/â€¦/%2~|%3~)"
local _usercol
if [[ $EUID -lt 1000 ]]; then
  # red for root, yellow for system users
  _usercol="%(!.%F{red}.%F{154})"
else
  _usercol="%F{yellow}"
fi
local _user="%{$_usercol%}%n"

local _prompt
if [[ $SHLVL -gt 1 ]]; then
  _prompt="%F{reset_color%}ð "
else
  _prompt="%F{reset_color%}ðº"
fi

if type "githud" > /dev/null; then
  PROMPT='$_path %F{reset_color}$(gitHUD zsh) $_prompt%b%f%k%F{white} '
else
  autoload -Uz vcs_info
  zstyle ':vcs_info:*' enable git
  PROMPT='$_path %F{reset_color}${vcs_info_msg_0_} $_prompt%b%f%k%F{white} '
fi

local RPROMPT='$_time'
if [[ ! -z "$SSH_CLIENT" ]]; then
  RPROMPT="$RPROMPT â‡„" # ssh icon
fi

##
# Environment variables
#

# basedir defaults, in case they're not already set up.
# http://standards.freedesktop.org/basedir-spec/basedir-spec-latest.html
if [[ -z $XDG_DATA_HOME ]]; then
  export XDG_DATA_HOME=$HOME/.local/share
fi

if [[ -z $XDG_CONFIG_HOME ]]; then
  export XDG_CONFIG_HOME=$HOME/.config
fi

if [[ -z $XDG_CACHE_HOME ]]; then
  export XDG_CACHE_HOME=$HOME/.cache
fi

if [[ -z $XDG_DATA_DIRS ]]; then
  export XDG_DATA_DIRS=/usr/local/share:/usr/share
fi

if [[ -z $XDG_CONFIG_DIRS ]]; then
  export XDG_CONFIG_DIRS=/etc/xdg
else
  export XDG_CONFIG_DIRS=/etc/xdg:$XDG_CONFIG_DIRS
fi

##
# zsh config
#

# add ~/.config/zsh/completion to completion paths
# NOTE: this needs to be a directory with 0755 permissions, otherwise you will
# get "insecure" warnings on shell load!
# fpath=($XDG_CONFIG_HOME/zsh/completion $fpath)

# Keep 1000 lines of history within the shell and save it to ~/.cache/shell_history
HISTSIZE=1000
SAVEHIST=1000
HISTFILE=$XDG_CACHE_HOME/shell_history

# shell options
setopt PROMPT_SUBST
setopt autocd # assume "cd" when a command is a directory
setopt histignorealldups # Substitute commands in the prompt
setopt sharehistory # Share the same history between all shells
setopt promptsubst # required for git plugin
setopt histignorespace # Ignore command start with space
# setopt extendedglob
# Extended glob syntax, eg ^ to negate, <x-y> for range, (foo|bar) etc.
# Backwards-incompatible with bash, so disabled by default.

# Colors!

# 256 color mode
export TERM="xterm-256color"

##
# Completion system
#
# Global completion settings first
zstyle ":completion:*" use-cache on
zstyle ":completion:*" cache-path $XDG_CACHE_HOME/zsh
zstyle ":completion:*" auto-description "specify: %d"
zstyle ":completion:*" format "Completing %d"
zstyle ":completion:*" group-name ""
zstyle ":completion:*" menu select=2
zstyle ":completion:*:default" list-colors ${(s.:.)LS_COLORS}
zstyle ":completion:*" list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ":completion:*" matcher-list "" "m:{a-z}={A-Z}" "m:{a-zA-Z}={A-Za-z}" "r:|[._-]=* r:|=* l:|=*"
zstyle ":completion:*" select-prompt %SScrolling active: current selection at %p%s
zstyle ":completion:*:*:kill:*:processes" list-colors "=(#b) #([0-9]#)*=0=01;31"
zstyle ":completion:*:kill:*" command "ps -u $USER -o pid,%cpu,tty,cputime,cmd"

compdef man man

zstyle ':completion:*:*:man:*' completer _complete  # Simpler completer just for man
zstyle ':completion:*:*:man:*' require-match true   # Don't try completion unless it can find matches
zstyle ':completion:*:*:man:*' min-prefix-length 2
zstyle ':completion:*:*:man:*' insert-sections true
zstyle ':completion:*:*:man:*' menu select=long     # Menu only for long lists
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:manuals' accept-exact true

##
# Keybinds
#

# Use emacs-style keybindings
bindkey -e

bindkey "$terminfo[khome]" beginning-of-line # Home
bindkey "$terminfo[kend]" end-of-line # End
bindkey "$terminfo[kich1]" overwrite-mode # Insert
bindkey "$terminfo[kdch1]" delete-char # Delete
bindkey "$terminfo[kcub1]" backward-char # Left
bindkey "$terminfo[kcuf1]" forward-char # Right
# bindkey "$terminfo[kpp]" # PageUp
# bindkey "$terminfo[knp]" # PageDown
zmodload zsh/terminfo

# Bind shift-tab to backwards-menu
# NOTE this won't work on Konsole if the new tab button is shown
bindkey "\e[Z" reverse-menu-complete

# Make ctrl-v edit the current command line
autoload edit-command-line
zle -N edit-command-line
bindkey "^v" edit-command-line

# Make sure the terminal is in application mode, when zle is
# active. Only then are the values from $terminfo valid.
if (( ${+terminfo[smkx]} )) && (( ${+terminfo[rmkx]} )); then
  function zle-line-init {
    printf "%s" ${terminfo[smkx]}
  }
  function zle-line-finish {
    printf "%s" ${terminfo[rmkx]}
  }
  zle -N zle-line-init
  zle -N zle-line-finish
fi

# typing ... expands to ../.., .... to ../../.., etc.
rationalise-dot() {
  if [[ $LBUFFER = *.. ]]; then
    LBUFFER+=/..
  else
    LBUFFER+=.
  fi
}
zle -N rationalise-dot
bindkey . rationalise-dot
bindkey -M isearch . self-insert # history search fix

# Functions
#

# function shorternPwd() {
#   local current_path=${PWD/#$HOME/"~"}
#   local home_path="$(getUniqueFolder $HOME)"
#   local trunc_path="$(getUniqueFolder $PWD)"
#   [[ $current_path == "~"* ]] && current_path="~${trunc_path//${home_path}/}" || current_path="/${trunc_path}"
# }

# function getUniqueFolder() {
#   local trunc_path directory test_dir test_dir_length
#   local -a matching
#   local -a paths
#   local cur_path='/'
#   paths=(${(s:/:)1})
#   for directory in ${paths[@]}; do
#     test_dir=''
#     for (( i=0; i < ${#directory}; i++ )); do
#       test_dir+="${directory:$i:1}"
#       matching=("$cur_path"/"$test_dir"*/)
#       if [[ ${#matching[@]} -eq 1 ]]; then
#         break
#       fi
#     done
#     trunc_path+="$test_dir/"
#     cur_path+="$directory/"
#   done
#   echo "${trunc_path: : -1}"
# }

function mkcd() {
  mkdir "$1"
  cd "$1"
}

# print a separator banner, as wide as the terminal
function hr {
  print ${(l:COLUMNS::=:)}
}

# Download Gist

function gistDownload() {
  curl -L ${1}/download | tar -xvz --strip-components=1
}

function precmd() {
    # Clear the terminal title before running any command.
    # This is a somewhat hacky workaround to Neovim not refreshing the title
    # after it exits.
    echo -ne "\e]1;${PWD##*/}\a"
}

# source and configs

# ANSIBLE
export ANSIBLE_NOCOWS=1
export OBJC_DISABLE_INITIALIZE_FORK_SAFETY=YES

export ERL_AFLAGS="-kernel shell_history enabled"

# KERL
export KERL_BUILD_DOCS=yes
export KERL_INSTALL_MANPAGES=no

# zoxide
[ -f $homebrew_prefix[$CPUTYPE]/bin/zoxide ] && eval "$(zoxide init zsh --cmd j)"

# source homebrew github api token
if [[ -f ~/.homebrew_github_api_token ]]; then
  source ~/.homebrew_github_api_token
fi

source ~/.dotfiles/zsh/aliasrc

# source google-cloud-sdk
if [[ -s $homebrew_prefix[$CPUTYPE]/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/path.zsh.inc ]]; then
  source $homebrew_prefix[$CPUTYPE]/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/path.zsh.inc
  source $homebrew_prefix[$CPUTYPE]/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/completion.zsh.inc
fi


# fzf
if [[ $TERM_PROGRAM != "WarpTerminal" ]]; then
  source <(fzf --zsh)

  export FZF_DEFAULT_COMMAND="fd --hidden --strip-cwd-prefix --exclude .git"
  export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
  export FZF_ALT_C_COMMAND="fd --type=d --hidden --strip-cwd-prefix --exclude .git"
  export FZF_CTRL_T_OPTS="--preview 'bat -n --color=always --line-range :500 {}'"
  export FZF_ALT_C_OPTS="--preview 'eza --tree --color=always {} | head -200'"


  # Use fd (https://github.com/sharkdp/fd) instead of the default find
  # command for listing path candidates.
  # - The first argument to the function ($1) is the base path to start traversal
  # - See the source code (completion.{bash,zsh}) for the details.
  _fzf_compgen_path() {
    fd --hidden --follow --exclude ".git" . "$1"
  }

  # Use fd to generate the list for directory completion
  _fzf_compgen_dir() {
    fd --type d --hidden --follow --exclude ".git" . "$1"
  }

  show_file_or_dir_preview="if [ -d {} ]; then eza --tree --color=always {} | head -200; else bat -n --color=always --line-range :500 {}; fi"
  # Advanced customization of fzf options via _fzf_comprun function
  # - The first argument to the function is the name of the command.
  # - You should make sure to pass the rest of the arguments to fzf.
  _fzf_comprun() {
    local command=$1
    shift

    case "$command" in
      cd)           fzf --preview 'eza --tree --color=always {} | head -200' "$@" ;;
      export|unset) fzf --preview "eval 'echo ${}'"         "$@" ;;
      ssh)          fzf --preview 'dig {}'                   "$@" ;;
      *)            fzf --preview "$show_file_or_dir_preview" "$@" ;;
    esac
  }
fi

# tre
if [[ -f $homebrew_prefix[$CPUTYPE]/bin/tre ]]; then
  tre() { command tre "$@" -e && source "/tmp/tre_aliases_$USER" 2>/dev/null; }
fi

# rust
if [ ! -f "$HOME/.config/rustlang/autocomplete/rustup" ]; then
  mkdir -p ~/.config/rustlang/autocomplete
  rustup completions bash rustup >> ~/.config/rustlang/autocomplete/rustup
fi

# idris2
if [[ -f ~/.idris2/bin/idris2 ]]; then
  eval "$(idris2 --bash-completion-script idris2)"
fi

# ghcup
[ -f "/Users/tai/.ghcup/env" ] && source "/Users/tai/.ghcup/env" # ghcup-env

export PATH="/opt/homebrew/opt/llvm@12/bin:$PATH"

# mise
eval "$($HOME/.local/bin/mise activate zsh)"

# fzf-git
[[ -f "$HOME/.dotfiles/fzf-git/fzf-git.sh" ]] && source "$HOME/.dotfiles/fzf-git/fzf-git.sh"

export BAT_THEME=DarkNeon

# iterm shell integration
if [[ $TERM_PROGRAM == "iTerm.app" ]]; then
  test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"
fi

# call zprof at the end of file
# zprof
